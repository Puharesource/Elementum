subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    group = 'io.puharesource.mc'
    version = ''

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    sourceSets {
        main {
            groovy {
                srcDirs = ['src/main/groovy']
            }
        }
    }

    task copyToJars(dependsOn: 'build', type: Copy) {
        from 'build/libs/'
        into "../../jars/"
    }

    configurations {
        provided
        compile.extendsFrom provided
    }

    repositories {
        mavenCentral()

        maven {
            name 'Spigot'
            url 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }

        maven {
            name 'Vault'
            url 'http://nexus.theyeticave.net/content/repositories/pub_releases'
        }
    }

    dependencies {
        provided 'org.bukkit:bukkit:1.8.3-R0.1-SNAPSHOT'

        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    artifacts {
        archives jar
    }

    jar {
        dependsOn configurations.runtime
        from {
            (configurations.runtime - configurations.provided).collect {
                it.isDirectory() ? it : zipTree(it)
            }
        } {
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }
    }

    if (!JavaVersion.current().isJava7Compatible()) {
        logger.lifecycle("WARNING! You're trying to compile using an unsupported Java version! Please use JDK 1.7 or above!")
    }
}